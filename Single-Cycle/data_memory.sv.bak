module data_memory (
    input  wire        clk,
    input  wire [31:0] address,
    input  wire [31:0] DMWR,
    input  wire [2:0]  DMCTRL,
    input  wire        mem_write,
    output reg  [31:0] Datard
);

    reg [31:0] memory [0:1023];
    
    wire [9:0] word_addr = address[11:2];
    wire [1:0] byte_offset = address[1:0];

    initial begin
        integer i;
        for (i = 0; i < 1024; i = i + 1) begin
            memory[i] = 32'h00000000;
        end
    end

    always @(*) begin
        case (DMCTRL)
            3'b000: begin
                case (byte_offset)
                    2'b00: Datard = {{24{memory[word_addr][7]}}, memory[word_addr][7:0]};
                    2'b01: Datard = {{24{memory[word_addr][15]}}, memory[word_addr][15:8]};
                    2'b10: Datard = {{24{memory[word_addr][23]}}, memory[word_addr][23:16]};
                    2'b11: Datard = {{24{memory[word_addr][31]}}, memory[word_addr][31:24]};
                endcase
            end
            
            3'b001: begin
                case (byte_offset[1])
                    1'b0: Datard = {{16{memory[word_addr][15]}}, memory[word_addr][15:0]};
                    1'b1: Datard = {{16{memory[word_addr][31]}}, memory[word_addr][31:16]};
                endcase
            end
            
            3'b010: begin
                Datard = memory[word_addr];
            end
            
            3'b100: begin
                case (byte_offset)
                    2'b00: Datard = {24'b0, memory[word_addr][7:0]};
                    2'b01: Datard = {24'b0, memory[word_addr][15:8]};
                    2'b10: Datard = {24'b0, memory[word_addr][23:16]};
                    2'b11: Datard = {24'b0, memory[word_addr][31:24]};
                endcase
            end
            
            3'b101: begin
                case (byte_offset[1])
                    1'b0: Datard = {16'b0, memory[word_addr][15:0]};
                    1'b1: Datard = {16'b0, memory[word_addr][31:16]};
                endcase
            end
            
            default: begin
                Datard = 32'h00000000;
            end
        endcase
    end

    always @(posedge clk) begin
        if (mem_write) begin
            case (DMCTRL)
                3'b000: begin
                    case (byte_offset)
                        2'b00: memory[word_addr][7:0]   <= DMWR[7:0];
                        2'b01: memory[word_addr][15:8]  <= DMWR[7:0];
                        2'b10: memory[word_addr][23:16] <= DMWR[7:0];
                        2'b11: memory[word_addr][31:24] <= DMWR[7:0];
                    endcase
                end
                
                3'b001: begin
                    case (byte_offset[1])
                        1'b0: memory[word_addr][15:0]  <= DMWR[15:0];
                        1'b1: memory[word_addr][31:16] <= DMWR[15:0];
                    endcase
                end
                
                3'b010: begin
                    memory[word_addr] <= DMWR;
                end
                
                default: begin
                end
            endcase
        end
    end

endmodule